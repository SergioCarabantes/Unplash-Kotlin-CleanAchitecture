apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.sergio.unplash"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        vectorDrawables.useSupportLibrary = true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {
    def projectDependencies = rootProject.ext.projectDependencies

    //Application projectDependencies
    implementation projectDependencies.kotlin
    implementation projectDependencies.appCompat
    implementation projectDependencies.supportLibraryDesign
    implementation projectDependencies.recyclerView
    implementation projectDependencies.constraintLayout
    implementation projectDependencies.cardView
    implementation projectDependencies.lifecycle
    implementation projectDependencies.roomRuntime
    kapt projectDependencies.roomCompiler
    implementation projectDependencies.pagination
    implementation projectDependencies.paginationrx
    kapt projectDependencies.daggerCompiler
    implementation projectDependencies.dagger
    annotationProcessor projectDependencies.daggerAndroidProcessor
    implementation projectDependencies.daggerAndroid
    implementation projectDependencies.rxJava
    implementation projectDependencies.rxAndroid
    implementation projectDependencies.retrofitRxJava2
    implementation projectDependencies.gson
    implementation projectDependencies.okhttp
    implementation projectDependencies.okhttpLoggingInterceptor
    implementation projectDependencies.retrofit
    implementation projectDependencies.retrofitGson
    implementation projectDependencies.timberLogging
    implementation projectDependencies.glide
    annotationProcessor projectDependencies.glideProcessor
    ktlint projectDependencies.ktlint

    //test
    testImplementation projectDependencies.jUnit
    testImplementation projectDependencies.jUnitParams
    testImplementation projectDependencies.mockito
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--reporter=plain",
            "--reporter=checkstyle,output=${buildDir}/reports/ktlint/checkstyle.xml",
            "${projectDir}/**/src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
